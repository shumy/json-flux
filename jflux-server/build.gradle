plugins {
  id 'java'
  id 'org.xtext.xtend' version '1.0.17'
  id 'org.dm.bundle' version '0.10.0'
  id 'maven-publish'
}

group 'com.github.shumy'
version '1.0'

sourceCompatibility = 1.8

defaultTasks 'deployBundle'
repositories.mavenCentral()

sourceSets {
  main {
    java.srcDirs= ['src/main']
    xtendOutputDir = 'build/xtend-gen'
  }
  test {
    java.srcDirs= ['src/test']
    xtendOutputDir = 'build/test/xtend-gen'
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      
      pom.withXml {
        def root = asNode()
        root.appendNode('packaging', 'jar')
        root.appendNode('name', 'Json-Flux - Main bundle to use in jflux services')
        //root.appendNode('description', description)
        root.appendNode('url', 'https://github.com/shumy/json-flux')
        
        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')
        
        def developer = root.appendNode('developers').appendNode('developer')
        developer.appendNode('id', 'shumy')
        developer.appendNode('name', 'Micael Pedrosa')
        developer.appendNode('email', 'micaelpedrosa@gmail.com')
        
        def scm = root.appendNode('scm')
        scm.appendNode('connection', 'scm:git:git@github.com:shumy/json-flux.git')
        scm.appendNode('developerConnection', 'scm:git:git@github.com:shumy/json-flux.git')
        scm.appendNode('url', 'git@github.com:shumy/json-flux.git')
      }
    }
  }
}

dependencies {
  compile 'org.slf4j:slf4j-api:[0.0.0, 1.7.0)'
  compile 'org.eclipse.xtend:org.eclipse.xtend.lib:[2.11.0, 3.0)'
  
  compile 'org.osgi:org.osgi.service.component.annotations:[1.3.0, 2.0)'
  compile 'org.osgi:org.osgi.framework:[1.8.0, 2.0)'
  
  compile 'io.netty:netty-handler:4.1.9.Final'
  compile 'io.netty:netty-codec-http:4.1.9.Final'
  
  compile 'com.fasterxml.jackson.core:jackson-core:2.8.8'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
  compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.8'
  
  compile 'info.picocli:picocli:0.9.5'
}

bundle {
  instruction '-dsannotations', '*'
  instruction 'Bundle-Name', 'Json-Flux'
  instruction 'Export-Package', 'com.github.shumy.jflux.api.*'
}

task deployBundle(type: Copy) {
  from jar
  into '../jflux-osgi-runtime/runtime/bundle'
}
