plugins {
  id 'java'
  id 'maven-publish'
  id 'org.xtext.xtend' version '1.0.17'       // http://xtext.github.io/xtext-gradle-plugin/xtend.html
  id 'org.dm.bundle' version '0.10.0'         // https://github.com/TomDmitriev/gradle-bundle-plugin
  id 'com.athaydes.osgi-run' version '1.5.4'  // https://github.com/renatoathaydes/osgi-run
}

group 'com.github.shumy'
version '1.0'

sourceCompatibility = 1.8

defaultTasks 'createBundlesDir'
repositories.mavenCentral()

sourceSets {
  main {
    java.srcDirs= ['src/main']
    xtendOutputDir = 'build/xtend-gen'
  }
  test {
    java.srcDirs= ['src/test']
    xtendOutputDir = 'build/test/xtend-gen'
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      
      pom.withXml {
        def root = asNode()
        root.appendNode('packaging', 'jar')
        root.appendNode('name', 'Json-Flux - Main bundle to use in jflux services')
        //root.appendNode('description', description)
        root.appendNode('url', 'https://github.com/shumy/json-flux')
        
        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')
        
        def developer = root.appendNode('developers').appendNode('developer')
        developer.appendNode('id', 'shumy')
        developer.appendNode('name', 'Micael Pedrosa')
        developer.appendNode('email', 'micaelpedrosa@gmail.com')
        
        def scm = root.appendNode('scm')
        scm.appendNode('connection', 'scm:git:git@github.com:shumy/json-flux.git')
        scm.appendNode('developerConnection', 'scm:git:git@github.com:shumy/json-flux.git')
        scm.appendNode('url', 'git@github.com:shumy/json-flux.git')
      }
    }
  }
}

dependencies {
  systemLib 'org.ops4j.pax.logging:pax-logging-api:1.9.1'
  systemLib 'org.eclipse.xtend:org.eclipse.xtend.lib:2.11.0'
  
  systemLib 'com.fasterxml.jackson.core:jackson-core:2.8.8'
  systemLib 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
  systemLib 'com.fasterxml.jackson.core:jackson-annotations:2.8.8'
  
  systemLib 'org.osgi:org.osgi.service.component.annotations:1.3.0'
  systemLib 'org.osgi:org.osgi.framework:1.8.0'
  
  osgiRuntime 'org.apache.felix:org.apache.felix.configadmin:1.8.14'
  osgiRuntime 'org.apache.felix:org.apache.felix.scr:2.0.8', { transitive = false }
  
  compile 'info.picocli:picocli:0.9.5'
  compile 'io.netty:netty-handler:4.1.9.Final'
  compile 'io.netty:netty-codec-http:4.1.9.Final'
}

bundle {
  instruction '-dsannotations', '*'
  instruction 'Bundle-Name', 'Json-Flux'
  instruction 'Export-Package', 'com.github.shumy.jflux.api.*'
}

runOsgi {
  config += [ 'org.osgi.framework.system.packages.extra': 'sun.misc' ]
  bundles += [ project ]
  outDir = '../runtime'
}
